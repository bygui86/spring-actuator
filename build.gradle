// *** SpringActuator - Sample ***

// SCRIPT
buildscript {
	ext {
		springBootVersion = '2.0.1.RELEASE'
		slf4jVersion = '1.7.25'
		lombokVersion = '1.16.20'
		dockerPluginVersion = '1.2'
		aptPluginVersion = '0.4'
	}

	repositories {
		mavenCentral()
		maven { url 'https://plugins.gradle.org/m2/' }
	}

	dependencies {
		classpath group: 'org.springframework.boot', name: 'spring-boot-gradle-plugin', version: "${springBootVersion}"
		classpath group: 'se.transmode.gradle', name: 'gradle-docker', version: "${dockerPluginVersion}"
		classpath group: 'net.ltgt.gradle', name: 'gradle-apt-plugin', version: "${aptPluginVersion}"
	}
}

// PLUGINS
apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'net.ltgt.apt'
apply plugin: 'docker'

// COMPILING
group 'com.rabbitshop'
version '0.1'


jar {
	baseName = 'springactuatorsample'
	version = '0.1'
}

// See https://docs.gradle.org/current/dsl/org.gradle.api.tasks.compile.JavaCompile.html
compileJava {
	// The Java language level to use to compile the source files.
	sourceCompatibility = 1.8

	// The target JVM to generate the .class files for.
	targetCompatibility = 1.8

	// PLEASE NOTE: with the new version of Gradle, Lombok needs Java9+ to compile properly with fork enabled
	// Enable compilation in a separate daemon process
	// options.fork = true

	// Enable incremental compilation
	options.incremental = true

	// Reveal specific warnings during compilation
	options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
}

// REPOS
repositories {
	mavenCentral()
}

// DEPENDENCIES
dependencies {

	// Spring Boot
	compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: "${springBootVersion}"

	// Spring Boot DevTools
	runtime group: 'org.springframework.boot', name: 'spring-boot-devtools', version: "${springBootVersion}"

	// Spring Web
//	compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: "${springBootVersion}"

	// Slf4j
	compile group: 'org.slf4j', name: 'slf4j-api', version: "${slf4jVersion}"

	// Lombok
	compileOnly group: 'org.projectlombok', name: 'lombok', version: "${lombokVersion}"
//	apt group: 'org.projectlombok', name: 'lombok', version: "${lombokVersion}"
	annotationProcessor group: 'org.projectlombok', name: 'lombok', version: "${lombokVersion}"
}

// TASKS
task wrapper(type: Wrapper) {
	gradleVersion = '4.7'
}

task image(type: Docker, dependsOn: build) {
	push = false
	applicationName = jar.baseName
	dockerfile = file('docker/Dockerfile')
	doFirst {
		copy {
			from jar
			into stageDir
		}
	}
}
